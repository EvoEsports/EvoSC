{contentType text}
<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="EvoSC:player-markers" id="player-markers" version="3">
    <script><!--
    #Include "TextLib" as TL

    #Struct EvoSC_Marker2 {
        Text login;
        Text name;
        Text prefix;
        Text color;
    }

    Boolean isTimeAttack(){
        return CurrentServerModeName == "TimeAttack" || CurrentServerModeName == "TM_TimeAttack_Online";
    }

    Boolean isTeams(){
        return UseClans;
    }

    Void setMarkersInit(EvoSC_Marker2[] markers){
        declare Text markersXml = "";
        foreach(marker in markers){
            markersXml = markersXml ^ "<marker label='"^marker.name^"' playerlogin='" ^ marker.login ^ "' />";
        }
        ClientUI.MarkersXML = markersXml;
    }

    Text escapeQuotes(Text text){
        return TL::Replace(text, "\"", "''");
    }

    Void setMarkers(EvoSC_Marker2[] markers){
        declare Text markersXml = "";
        declare isTimeAttack = isTimeAttack();
        declare isTeams = isTeams();

        foreach(marker in markers){
            if(LocalUser.Login == marker.login){
                continue;
            }
            markersXml = markersXml ^ """<marker label="$<""" ^  escapeQuotes(marker.name) ^ """$>" playerlogin='""" ^ marker.login ^ """' box='0 0.6 0' distmax='95' color='{config('theme.hud.bg-light')}' />""";
        }

        ClientUI.AlliesLabelsVisibility = CUIConfig::ELabelsVisibility::Never;
        ClientUI.TeamLabelsVisibility = CUIConfig::ELabelsVisibility::Never;
        ClientUI.OpposingTeamLabelsVisibility = CUIConfig::ELabelsVisibility::Never;
        ClientUI.OverrideMarkers = True;
        ClientUI.MarkersXML = markersXml;
    }

    main(){
        declare EvoSC_Marker2[] EvoSC_MarkerNames2 for This;
        declare Integer EvoSC_MarkerNames_Update2 for This;
        declare lastUpdate = 0;
        declare initialized = False;
        declare currentMapUid = "";

        wait(InputPlayer != Null);

        while(True){
            yield;

            if(lastUpdate != EvoSC_MarkerNames_Update2){
                lastUpdate = EvoSC_MarkerNames_Update2;
                if(initialized == False){
                    initialized = True;
                    setMarkersInit(EvoSC_MarkerNames2);
                    yield;
                }
                setMarkers(EvoSC_MarkerNames2);
            }
        }
    }
    --></script>
</manialink>