{contentType text}
<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="EvoSC:dark-souls" id="dark-souls" version="3">
    <frame>
        <quad id="quad" pos="-160 90" size="320 180" bgcolor="000" opacity="0.75" />
        <label id="label" text="YOU DIED" textsize="10" opacity="0" textcolor="f00" valign="center" halign="center" textfont="{config('theme.font')}" z-index="1" />
    </frame>

    <script><!--
    main() {
        declare quad <=> (Page.MainFrame.GetFirstChild("quad") as CMlQuad);
        declare label <=> (Page.MainFrame.GetFirstChild("label") as CMlLabel);
        declare active = False;

        AnimMgr.Add(quad, "<quad opacity='0.0' />", 2000, CAnimManager::EAnimManagerEasing::QuadOut);

        while(True){
            yield;

            foreach(Event in RaceEvents){
                if(Event.Type == CTmRaceClientEvent::EType::Respawn){
                    if(Event.Player.User.Login == LocalUser.Login){
                        active = False;
                        AnimMgr.Flush(label);
                        quad.Opacity = 0.0;
                        label.Opacity = 0.0;
                    }
                }
                if(!active && (Event.Type == CTmRaceClientEvent::EType::Impact || Event.Type == CTmRaceClientEvent::EType::GlassSmash)){
                    if(Event.Player.User.Login == LocalUser.Login){
                        active = True;
                        label.Scale = 0.5;
                        label.Opacity = 0.1;
                        AnimMgr.Add(label, "<label scale='1.5' opacity='1.0' />", 4000, CAnimManager::EAnimManagerEasing::QuadOut);
                        quad.Opacity = 1.0;
                    }
                }
            }
        }
    }
    --></script>
</manialink>
