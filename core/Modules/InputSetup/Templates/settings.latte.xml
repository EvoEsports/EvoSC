{contentType text}
<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="EvoSC:key-binds-settings" id="key-binds-settings" version="3">
    {include isManiaPlanet() ? 'Components.stylesheet' : 'Components.stylesheet_noblur'}
    <frame id="window" pos="-30 {(count($binds) * 4.0 + 16.0)/2.0}">
        <quad class="bg-dark" size="60 {count($binds) * 4.0 + 16.0}" opacity="0.8" z-index="-2"/>
        <quad class="bg-accent" size="60 6" z-index="-3"/>
        <quad class="bg-accent" size="60 0.4" pos="0 -6" valign="center" z-index="1"/>
        <quad id="handle" data-id="keybinds" size="55 6"/>
        <label class="text-light" pos="1.5 -3" valign="center" textsize="0.5" textprefix="$s" text="ðŸŽ® Input-Setup" />
        <label class="close" size="6 6" pos="57 -3" valign="center" halign="center" textsize="0.5" textprefix="$s" text="ï€" ScriptEvents="1" focusareacolor1="0000" focusareacolor2="0003" />

        <frame id="entries">
            {foreach $binds as $key => $bind}
            <label class="select btn-default" size="8 4" pos="6 {$key * -4 - 10}" textsize="0.5" valign="center" halign="center" data-default="{$bind['default']}" data-id="{$bind['id']}" scale="0.9" ScriptEvents="1" />
            {/foreach}
        </frame>

        {foreach $binds as $key => $bind}
        <label class="text-light" text="{$bind['description']}" pos="11 {$key * -4 - 10}" size="50 4" valign="center" textsize="0.75" />
        {/foreach}

        <label class="save btn-success" text="Save & close" size="20 5" pos="30 {count($binds) * -4.0 - 12.0}" valign="center" halign="center" textsize="0.5" ScriptEvents="1" focusareacolor1="{config('colors.ui.window.header.color')}3" focusareacolor2="{config('colors.ui.window.header.color')}c" scale="0.9" />

        <frame id="overlay" z-index="100" hidden="1">
            <quad class="bg-dark" size="60 {count($binds) * 4.0 + 10.0}" pos="0 -6" />
            <label class="text-accent" pos="30 {(count($binds) * 4.0 + 16.0)/-2+3}" valign="center" halign="center" textsize="5" textprefix="$s" text="ðŸŽ®" z-index="1" />
            <label id="overlay-text" class="text-light" pos="30 {(count($binds) * 4.0 + 16.0)/-2-3}" valign="center" halign="center" textsize="0.5" textprefix="$sPress key to bind" z-index="1" />
        </frame>
    </frame>

    <script><!--
    #Include "MathLib" as ML
    #Include "TextLib" as TL

    #Struct Bind {
        Integer code;
        Text name;
        Text def;
        Text id;
    }

    {(new EvoSC\Classes\ManiaLinkDrag())|noescape}

    --></script>

    {include 'Scripts.mle'}

    <script><!--
    main(){
        declare Text[Text] CustomKeyBinds for LocalUser;
        declare window <=> (Page.MainFrame.GetFirstChild("window") as CMlFrame);
        declare entries <=> (Page.MainFrame.GetFirstChild("entries") as CMlFrame);

        foreach(control in entries.Controls){
            declare entry = (control as CMlLabel);
            declare id = entry.DataAttributeGet("id");
            if(CustomKeyBinds.existskey(id)){
                entry.Value = CustomKeyBinds[id];
                entry.DataAttributeSet("old", entry.Value);
            }else{
                entry.Value = entry.DataAttributeGet("default");
                entry.DataAttributeSet("old", entry.Value);
            }
        }

        while(True){
            yield;

            foreach(event in PendingEvents){
                if(event.Control == Null) continue;

                if(event.Control.HasClass("save") && event.Type == CMlScriptEvent::Type::MouseClick){
                    declare Bind bind;
                    foreach(control in entries.Controls){
                        declare label = (control as CMlLabel);
                        declare id = label.DataAttributeGet("id");
                        declare bindString = label.DataAttributeGet("bind");

                        if(bindString != ""){
                            CustomKeyBinds[id] = label.Value;
                            ManiaLinkEvent("update_bind", bindString);
                        }
                    }
                    TriggerPageAction("update_keybinds");
                    window.Hide();
                    return;
                }

                if(event.Control.HasClass("select") && event.Type == CMlScriptEvent::Type::MouseClick){
                    declare label = (event.Control as CMlLabel);
                    declare id = label.DataAttributeGet("id");
                    declare old = label.DataAttributeGet("old");
                    declare def = label.DataAttributeGet("default");
                    declare Bind bind;

                    (Page.MainFrame.GetFirstChild("overlay") as CMlFrame).Show();
                    (Page.MainFrame.GetFirstChild("overlay-text") as CMlLabel).Value = "\n$aaa$ipress " ^ old ^ " to abort";

                    while(bind.name == ""){
                        yield;
                        foreach(event in PendingEvents){
                            if(event.Type == CMlScriptEvent::Type::KeyPress){
                                bind.code = event.KeyCode;
                                bind.name = event.KeyName;
                                bind.def = def;
                                bind.id = id;
                                label.Value = bind.name;
                            }
                        }
                    }

                    label.DataAttributeSet("bind", bind.tojson());
                    (Page.MainFrame.GetFirstChild("overlay") as CMlFrame).Hide();

                    continue;
                }

                if(event.Control.HasClass("close") && event.Type == CMlScriptEvent::Type::MouseClick){
                    window.Hide();
                    return;
                }
            }

            maniaLinkDrag();
        }
    }
    --></script>
</manialink>