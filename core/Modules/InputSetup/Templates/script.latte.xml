{contentType text}
<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="EvoSC:key-binds" id="key-binds" version="3">
    <script><!--
    #Include "MathLib" as ML
    #Include "TextLib" as TL

    #Struct Bind {
        Integer code;
        Text name;
        Text def;
        Text id;
    }

    main(){
        declare Bind[] KeyBinds;
        declare lastKeyStroke = 0;
        declare spamCounter = 0;

        KeyBinds.fromjson("""{$binds->toJson()|noescape}""");

        while (True) {
            yield;

            foreach (event in PendingEvents) {
                if(event.Type == CMlScriptEvent::Type::KeyPress){
                    foreach(bind in KeyBinds){
                        if(bind.code != 0){
                            if(event.KeyCode == bind.code){
                                if(GameTime - lastKeyStroke < 40){
                                    spamCounter += 40;
                                }else{
                                    if(spamCounter > 0){
                                        if(GameTime - lastKeyStroke > 3000){
                                            spamCounter = 0;
                                        }else{
                                            spamCounter -= (GameTime - lastKeyStroke);
                                            if(spamCounter < 0) spamCounter = 0;
                                        }
                                    }
                                }

                                lastKeyStroke = GameTime;

                                if(spamCounter > 20){
                                    sleep(750);
                                    break;
                                }

                                TriggerPageAction("bound_key_pressed," ^ bind.id);
                                break;
                            }
                        }else{
                            if(event.KeyName == bind.def){
                                TriggerPageAction("bound_key_pressed," ^ bind.id);
                                break;
                            }
                        }
                    }
                }
            }
        }
    }
    --></script>
</manialink>