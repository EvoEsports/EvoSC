{contentType text}
<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="ESC:anti-rounds-afk" id="anti-rounds-afk" version="3">
    <script><!--
    Boolean check(){
        declare startDistance = InputPlayer.Distance;
        declare startTime = GameTime;

        while(GameTime - startTime < 10000){
            yield;

            if(InputPlayer.Distance - startDistance > 5){
                return False;
            }
        }

        return True;
    }

    Boolean isAfk(){
        declare startTime = GameTime;

        wait(InputPlayer.RaceState == CTmMlPlayer::ERaceState::Running);
        while(InputPlayer.RaceState == CTmMlPlayer::ERaceState::Running){
            yield;

            foreach(Event in RaceEvents){
                if(Event.Type == CTmRaceClientEvent::EType::WayPoint){
                    if(Event.IsEndLap){
                        if(Event.Player.User.Login == LocalUser.Login){
                            return False;
                        }

                        return check();
                    }
                }
            }

            if(GameTime - startTime >= (Map.MapInfo.TMObjective_AuthorTime + 2000)){
                return check();
            }
        }

        return False;
    }

    main(){
        while(True){
            yield;
            if(InputPlayer == Null) continue;

            foreach(Event in RaceEvents){
                if(Event.Type == CTmRaceClientEvent::EType::Respawn){
                    if(Event.Player.User.Login == LocalUser.Login){
                        if(isAfk()){
                            TriggerPageAction("{$MODULE|classpath}::mlePutMeToSpec,null");
                        }
                    }
                }
            }

            if(CurrentServerModeName != "Rounds"){
                return;
            }
        }
    }
    --></script>
</manialink>
