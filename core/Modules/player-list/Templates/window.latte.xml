<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="ESC:PlayerList" id="PlayerList" version="3">
    <framemodel id="row">
        <quad pos="0 0" size="120 5" valign="center" bgcolor="000" z-index="-1" opacity="0.9" />
        <label pos="2 0" size="26 5" valign="center" textsize="0.75" text="-- nickname --" />
        <label pos="29 0" size="14 5" valign="center" textsize="0.75" text="-- login --" />
        <label class="profile" pos="51 0" size="12 3.5" valign="center" halign="center" textsize="0.5" text="ï€‡ Profile" ScriptEvents="1" focusareacolor1="0000" focusareacolor2="{config('colors.ui.header')}a" />

        <frame class="admin-buttons" pos="58 0">
            <label class="warn" pos="0 0" size="12 3.5" valign="center" halign="center" textsize="0.5" text="ï± Warn" ScriptEvents="1" focusareacolor1="0000" focusareacolor2="{config('colors.warning')}a" />
            <label class="kick" pos="13 0" size="12 3.5" valign="center" halign="center" textsize="0.5" text="ïˆµ Kick" ScriptEvents="1" focusareacolor1="0000" focusareacolor2="{config('colors.warning')}a" />
            <label class="ban" pos="26 0" size="12 3.5" valign="center" halign="center" textsize="0.5" text="ï– Ban" ScriptEvents="1" focusareacolor1="0000" focusareacolor2="{config('colors.warning')}a" />
            <label class="ban" pos="39 0" size="12 3.5" valign="center" halign="center" textsize="0.5" text="ï– Mute" ScriptEvents="1" focusareacolor1="0000" focusareacolor2="{config('colors.warning')}a" />
        </frame>
    </framemodel>

    <frame id="playerlist" pos="-60 42.5" size="120 180">
        <quad pos="0 0" size="120 5" bgcolor="{config('colors.ui.header')}" opacity="0.9" z-index="-1" />
        <quad id="handle" pos="0 0" size="115 5" z-index="5" />
        <quad id="bg" pos="0 -5" bgcolor="{config('colors.ui.background')}" opacity="0.8" z-index="-1" />
        <label pos="2 -2.5" textsize="0.8" text="ðŸ‘¥ Player List" textprefix="$s" valign="center" />
        <label class="close" pos="117.5 -2.5" size="5 5" textsize="0.2" text="âŒ" textprefix="$s" valign="center" halign="center" ScriptEvents="1" focusareacolor1="0000" focusareacolor2="0003" />

        <frame id="content" size="120 80" pos="0 -5" z-index="5">
            {for $i=1; $i<=20; $i++}
            <frameinstance modelid="row" pos="0 {$i * -5 + 2.5}"></frameinstance>
            {/for}
        </frame>

        <frame id="pagination" pos="4 -86.5" scale="0.9" z-index="3">
            <label id="page-info" pos="6 0" size="8 3.5" valign="center"
                   halign="center" text="1/1" textsize="0.8"/>
            <label class="previous-page" pos="0 0" size="3.5 3.5" valign="center" halign="center" text="â´"
                   textsize="1" ScriptEvents="1" focusareacolor1="{config('colors.ui.header')}3"
                   focusareacolor2="{config('colors.ui.header')}c"/>
            <label class="next-page" pos="12 0" size="3.5 3.5" valign="center" halign="center" text="âµ"
                   textsize="1" ScriptEvents="1" focusareacolor1="{config('colors.ui.header')}3"
                   focusareacolor2="{config('colors.ui.header')}c"/>
        </frame>
    </frame>

    <frame id="input-dialog" pos="-40 11" z-index="10" hidden="1">
        <quad size="80 22" bgcolor="{config('colors.ui.background')}" opacity="0.9" z-index="-1" />
        <quad size="80 2" bgcolor="{config('colors.ui.header')}" opacity="0.9" z-index="0" />
        <label pos="40 -6" size="76" valign="center" halign="center" text="" textsize="1" z-index="1" />
        <entry pos="40 -11" size="60 4" valign="center" halign="center" textsize="1" focusareacolor1="{config('colors.ui.header')}1" focusareacolor2="{config('colors.ui.header')}3" />

        <label class="confirm" pos="32 -17" z-index="0" size="15 5" text="Confirm" ScriptEvents="1" focusareacolor1="07E20099" halign="center" valign="center" textsize="0.5" focusareacolor2="06BD00AA" scriptevents="1"/>
        <label class="cancel" pos="48 -17" z-index="0" size="15 5" text="Cancel" ScriptEvents="1" focusareacolor1="f009" halign="center" valign="center" textsize="0.5" focusareacolor2="BD0000AA" scriptevents="1"/>
    </frame>

    <script><!--
#Include "MathLib" as ML
#Include "TextLib" as TL

declare CMlFrame playerlist;
declare CMlFrame content;
declare CMlQuad bg;
declare Integer page;

{(new esc\Classes\ManiaLinkDrag())|noescape}

Void updateList(Integer playerCount){
    declare sizeMultiplier = playerCount;
    if(sizeMultiplier > 20){
        sizeMultiplier = 20;
    }
    declare pagination <=> (Page.MainFrame.GetFirstChild("pagination") as CMlFrame);
    declare pageInfoLabel <=> (Page.MainFrame.GetFirstChild("page-info") as CMlLabel);
    bg.Size = <playerlist.Size[0], sizeMultiplier * 5.0 + 5.0>;
    content.Size = <bg.Size[0], sizeMultiplier * 5.0>;
    playerlist.RelativePosition_V3 = <-(bg.Size[0]/2), (bg.Size[1]/2) + 2.5>;
    pagination.RelativePosition_V3 = <4.0, sizeMultiplier * -5.0 - 7.5>;

    declare i = 0;
    declare skipped = 0;
    declare toSkip = page * 20;

    foreach(player in Players){
        if(skipped < toSkip){
            skipped += 1;
            continue;
        }

        declare row <=> (content.Controls[i] as CMlFrame);
        row.DataAttributeSet("login", player.User.Login);
        (row.Controls[1] as CMlLabel).SetText(player.User.Name);
        (row.Controls[2] as CMlLabel).SetText(player.User.Login);
        row.Show();

        i += 1;

        if(i >= content.Controls.count){
            return;
        }
    }

    if(i < 19){
        for(x, i, 19){
            declare row <=> (content.Controls[x] as CMlFrame);
            row.Hide();
        }
    }

    pageInfoLabel.SetText((page + 1) ^ "/" ^ ML::CeilingInteger(playerCount / 20.0));
}

Void setupAlternatingRowBackground(){
    declare Boolean light = True;
    declare Vec3 colorLight = <0.2, 0.2, 0.2>;
    declare Vec3 colorDark = <0.1, 0.1, 0.1>;
    foreach(control in content.Controls){
        if(light){
            ((control as CMlFrame).Controls[0] as CMlQuad).BgColor = colorLight;
        }else{
            ((control as CMlFrame).Controls[0] as CMlQuad).BgColor = colorDark;
        }
        light = !light;
    }
}

//Open input dialog and return decision
Text input(Text text){
    declare inputDialogFrame <=> (Page.MainFrame.GetFirstChild("input-dialog") as CMlFrame);
    declare Text input = "";
    declare Boolean decided = False;

    //show confirm dialog
    inputDialogFrame.Show();

    //Set question text
    (inputDialogFrame.Controls[2] as CMlLabel).SetText(text);

    while(!decided){
        yield;

        maniaLinkDrag();

        foreach(event in PendingEvents){
            if(event.Type == CMlScriptEvent::Type::MouseClick && event.Control.HasClass("confirm")){
                input = (inputDialogFrame.Controls[3] as CMlEntry).Value;
                decided = True;
            }
            if(event.Type == CMlScriptEvent::Type::MouseClick && event.Control.HasClass("cancel")){
                input = "-1";
                decided = True;
            }
        }
    }

    //hide input dialog
    inputDialogFrame.Hide();
    (inputDialogFrame.Controls[3] as CMlEntry).SetText("", False);

    return input;
}

main() {
    playerlist <=> (Page.MainFrame.GetFirstChild("playerlist") as CMlFrame);
    content <=> (Page.MainFrame.GetFirstChild("content") as CMlFrame);
    bg <=> (Page.MainFrame.GetFirstChild("bg") as CMlQuad);

    setupAlternatingRowBackground();

    declare playerCount = -1;
    page = 0;

    while(True){
        yield;

        if(playerCount != Players.count){
            playerCount = Players.count;
            updateList(playerCount);
        }

        foreach(event in PendingEvents){
            if(event.Type == CMlScriptEvent::Type::MouseClick && event.Control.HasClass("close")){
                playerlist.Hide();
            }

            if(event.Type == CMlScriptEvent::Type::MouseClick && event.Control.HasClass("previous-page")){
                page -= 1;
                if(page < 0){
                    page = ML::FloorInteger(Players.count / 20.0);
                }
                updateList(playerCount);
            }

            if(event.Type == CMlScriptEvent::Type::MouseClick && event.Control.HasClass("next-page")){
                page += 1;
                if(page > (Players.count / 20)){
                    page = 0;
                }
                updateList(playerCount);
            }

            if(event.Type == CMlScriptEvent::Type::MouseClick && event.Control.HasClass("profile")){
                declare login = event.Control.Parent.DataAttributeGet("login");
                TriggerPageAction("profile," ^ login);
            }

            if(event.Type == CMlScriptEvent::Type::MouseClick && event.Control.HasClass("kick")){
                declare login = event.Control.Parent.Parent.DataAttributeGet("login");
                declare reason = input("Enter a reason or leave empty");
                if(reason != "-1"){
                    TriggerPageAction("kick," ^ login ^ "," ^ reason);
                }
            }

            if(event.Type == CMlScriptEvent::Type::MouseClick && event.Control.HasClass("warn")){
                declare login = event.Control.Parent.Parent.DataAttributeGet("login");
                declare reason = input("Enter a reason or leave empty");
                if(reason != "-1"){
                    TriggerPageAction("warn," ^ login ^ "," ^ reason);
                }
            }

            if(event.Type == CMlScriptEvent::Type::MouseClick && event.Control.HasClass("ban")){
                declare login = event.Control.Parent.Parent.DataAttributeGet("reason");
                declare reason = input("Enter a reason or leave empty");
                if(reason != "-1"){
                    declare length = input("Enter Length. Empty = perm. (12h = 12hours, 1d6h = 1day+6hours, m=min, mo=months, ...)");
                    if(length != "-1"){
                        TriggerPageAction("ban," ^ login ^ "," ^ length ^ "," ^ reason);
                    }
                }
            }
        }

        maniaLinkDrag();
    }
}
    --></script>
</manialink>