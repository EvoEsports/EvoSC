{contentType text}
<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="ESC:{include id}" id="{include id}" version="3">
    <script><!--
#Include "MathLib" as ML
#Include "TextLib" as TL

#Struct WidgetRow {
    Real height;
    Boolean visible;
    Real[Integer] widths;
    Integer slot;
}

#Struct WidgetGrid {
    WidgetRow[Integer] left;
    WidgetRow[Integer] right;
}

#Struct UISettings {
    Boolean hide;
    Integer speed;
    Real scale;
    Boolean sb_blur;
}

{include globals}

declare CMlFrame widget;
declare CMlQuad bg;
declare Boolean left__;
declare Integer slot__;
--></script>

    <frame pos="0 0" id="widget" size="{include size}" z-index="10000">
        {include content}
    </frame>

    {if !isset($customHidescript)}
    {include 'scripts.hide'}
    {/if}

    <script><!--
    Void rearrange__(){
        declare WidgetGrid grid_ for This;
        declare WidgetRow[Integer] column;
        declare UISettings settings for This;
        declare Real scale = settings.scale;

        if(scale < 0.75){
            scale = 1.0;
        }
        if(scale > 1.2){
            scale = 1.2;
        }

        declare Real gutter = 1.0 * scale;
        widget.RelativeScale = scale;

        declare width = widget.Size[0] * widget.RelativeScale;
        declare height = widget.Size[1] * widget.RelativeScale;

        if(left__){
            widget.RelativePosition_V3[0] = -160.0;
            column = grid_.left;
        }else{
            widget.RelativePosition_V3[0] = 160.0 - width;
            column = grid_.right;
        }

        declare heights = 0.0;
        declare widths = 0.0;
        declare count = 0;

        for(i, 1, slot__ - 1){
            if(!column.existskey(i)){
                continue;
            }
            if(!column[i].visible){
                continue;
            }

            if(column[i].slot != slot__){
                heights += column[i].height * scale;
                count += 1;
            }
        }

        {if isset($slotHorizontal)}
        for(i, 1, {$slotHorizontal}){
            if(!column.existskey(slot__)){
                continue;
            }
            if(!column[slot__].widths.existskey(i)){
                continue;
            }
            widths += column[slot__].widths[i] + gutter;
        }
        widths *= scale;
        declare newX = 0.0;
        if(left__){
            newX = -160.0 + widths + gutter;
        }else{
            newX = 160.0 - widths + gutter;
        }
        {else}
        declare newX = widget.RelativePosition_V3[0];
        {/if}

        declare newY = 90.0 - heights - (gutter * count) - 2.5;
        HS_POS = <newX, newY>;
        if(widget.RelativePosition_V3[1] != newY){
            declare posString = newX ^ " " ^ newY;
            AnimMgr.Add(widget, """<frame pos='""" ^ posString ^ """' />""", 500, CAnimManager::EAnimManagerEasing::ExpOut);
        }
        sleep(50);
        if(widget.RelativePosition_V3[0] != newX){
            widget.RelativePosition_V3[0] = newX;
        }
    }

    Void setup__(){
        declare WidgetGrid grid_ for This;
        declare Integer LastUiUpdate for This = 0;

        if(left__){
            if(grid_.left.count == 0){
                grid_.left = WidgetRow[Integer];
            }
            if(!grid_.left.existskey(slot__)){
                declare WidgetRow wr;
                wr.height = 0.0;
                wr.visible = True;
                wr.slot = slot__;
                grid_.left[slot__] = wr;
            }
            declare height = widget.Size[1] * widget.RelativeScale;
            if(height > grid_.left[slot__].height){
                grid_.left[slot__].height = height;
            }
            grid_.left[slot__].visible = True;
            {if isset($slotHorizontal)}
            declare width = widget.Size[0] * widget.RelativeScale;
            grid_.left[slot__].widths[{$slotHorizontal}] = width;
            {/if}
        }else{
            if(grid_.right.count == 0){
                grid_.right = WidgetRow[Integer];
            }
            if(!grid_.right.existskey(slot__)){
                declare WidgetRow wr;
                wr.height = 0.0;
                wr.visible = True;
                wr.slot = slot__;
                grid_.right[slot__] = wr;
            }
            declare height = widget.Size[1] * widget.RelativeScale;
            if(height > grid_.right[slot__].height){
                grid_.right[slot__].height = height;
            }
            grid_.right[slot__].visible = True;
            {if isset($slotHorizontal)}
            declare width = widget.Size[0] * widget.RelativeScale;
            grid_.right[slot__].widths[{$slotHorizontal}] = width;
            {/if}
        }
    }
    --></script>

    {include script}

    <script><!--
    main() {
        declare WidgetGrid grid_ for This;
        declare Integer LastUiUpdate for This = 0;
        declare Integer lastUiUpdate;

        if(LastUiUpdate == 0){
            LastUiUpdate = GameTime;
            lastUiUpdate = LastUiUpdate;
        }

        widget <=> (Page.MainFrame.GetFirstChild("widget") as CMlFrame);
        bg <=> (Page.MainFrame.GetFirstChild("bg") as CMlQuad);

        {include bootScript}

        setup__();
        LastUiUpdate = GameTime;
        rearrange__();

        while(True){
            yield;

            foreach(event in PendingEvents){
                if(event.Control == Null) continue;

                {include pendingEvents}
            }

            {include loop}

            {if !isset($customHidescript) && !isset($dontHide)}
            hidescript();
            {/if}

            if(lastUiUpdate != LastUiUpdate){
                lastUiUpdate = LastUiUpdate;
                rearrange__();
            }
        }
    }
    --></script>
</manialink>
